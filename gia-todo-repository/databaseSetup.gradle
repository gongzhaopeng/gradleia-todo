buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.h2database:h2:2.1.214'
    }
}

ext.h2TcpPort = 9092
ext.h2TcpPassword = '::TcpPassword'
ext.h2StartupBlockMs = 2000

tasks.register('startDatabase', H2DatabaseStarter) {
    group = 'database'
    description = 'Starts database.'
    tcpPort = h2TcpPort
    tcpPassword = h2TcpPassword
    blockMs = h2StartupBlockMs
}

tasks.register('stopDatabase', JavaExec) {
    group = 'database'
    description = 'Stops database.'
    classpath = buildscript.configurations.classpath
    mainClass = 'org.h2.tools.Server'
    args = ['-tcpShutdown', "tcp://localhost:${h2TcpPort}", '-tcpPassword', h2TcpPassword]
}

tasks.register('buildSchema', JavaExec) {
    dependsOn 'startDatabase'

    group = 'database'
    description = 'Builds database schema.'
    classpath = buildscript.configurations.classpath
    workingDir = projectDir
    mainClass = 'org.h2.tools.RunScript'
    args = ['-url', 'jdbc:h2:~/todo', '-user', 'sa', '-script', 'create-todo.sql']
}

tasks.register('startAndPrepareDatabase') {
    dependsOn 'buildSchema'
}

class H2DatabaseStarter extends DefaultTask {
    @Input
    int tcpPort

    @Input
    String tcpPassword

    @Input
    int blockMs

    @TaskAction
    void start() {
        Thread.start { org.h2.tools.Server.main('-tcp', '-tcpPort', tcpPort.toString(), '-tcpPassword', tcpPassword) }
        Thread.sleep(blockMs)
    }
}