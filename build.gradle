import static org.gradle.api.tasks.testing.logging.TestLogEvent.*

allprojects {
    group = 'com.atzu68.learning.gia'
    version = '0.1'
}

subprojects {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation fileTree("${System.properties['user.home']}/Distribution/Groovy/SDK/groovy-4.0.9/lib") {
            include '*.jar'
            include '*/*.jar'
        }

        testImplementation group: 'org.testng', name: 'testng', version: '7.5'
        testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
    }

    def testNG = tasks.register('testNG', Test) {
        useTestNG()
    }

    test {
        dependsOn('testNG')
        useJUnitPlatform()

        systemProperty 'items', '20'
        minHeapSize = '128m'
        maxHeapSize = '256m'
        jvmArgs '-XX:MaxPermSize=128m'

        testLogging {
            showStandardStreams = true
            exceptionFormat 'full'
            events STARTED, PASSED, SKIPPED, FAILED
        }

        forkEvery = 5
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2)
    }

    tasks.register('aggregateTestReports', TestReport) {
        destinationDirectory = test.reports.html.outputLocation

//        reportOn test, testNG.get()
        testResults.from(test.binaryResultsDirectory, testNG.get().binaryResultsDirectory)
    }

    check.dependsOn 'aggregateTestReports'
}

tasks.named('wrapper') {
    gradleVersion = '7.5.1'
}

tasks.register('hello') {
    doLast {
        println 'Hello from root project'
    }
}